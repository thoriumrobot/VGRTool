plugins {
    id 'java'
    id 'application'
}

group 'com.example'
version '1.0'

repositories {
    mavenCentral()
}

dependencies {
    // Eclipse JDT Core
    implementation 'org.eclipse.jdt:org.eclipse.jdt.core:3.30.0'

    // Eclipse Text
    implementation 'org.eclipse.platform:org.eclipse.text:3.12.0'

    // Eclipse JFace Text
    implementation 'org.eclipse.platform:org.eclipse.jface.text:3.16.400'

    // Checker Framework Annotations
    implementation 'org.checkerframework:checker:3.48.1'
    implementation 'org.checkerframework:checker-qual:3.48.1'
    annotationProcessor 'org.checkerframework:checker:3.48.1'

    // javax.annotation API (for @Nullable)
    implementation 'javax.annotation:javax.annotation-api:1.3.2'

    // JUnit for testing (optional)
    testImplementation 'junit:junit:4.13.2'
}

application {
    mainClassName = 'VGRTool'
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
    options.compilerArgs += [
        '-Xlint:unchecked',
        '-Xlint:deprecation',

        // Required exports and opens for Checker Framework
        '--add-exports=jdk.compiler/com.sun.tools.javac.api=ALL-UNNAMED',
        '--add-exports=jdk.compiler/com.sun.tools.javac.code=ALL-UNNAMED',
        '--add-exports=jdk.compiler/com.sun.tools.javac.file=ALL-UNNAMED',
        '--add-exports=jdk.compiler/com.sun.tools.javac.main=ALL-UNNAMED',
        '--add-exports=jdk.compiler/com.sun.tools.javac.model=ALL-UNNAMED',
        '--add-exports=jdk.compiler/com.sun.tools.javac.processing=ALL-UNNAMED',
        '--add-exports=jdk.compiler/com.sun.tools.javac.tree=ALL-UNNAMED',
        '--add-exports=jdk.compiler/com.sun.tools.javac.util=ALL-UNNAMED',
        '--add-opens=jdk.compiler/com.sun.tools.javac.comp=ALL-UNNAMED'
    ]

    // Use annotationProcessorPath for the Checker Framework
    options.annotationProcessorPath = files("${System.getenv("CHECKERFRAMEWORK")}/checker/dist/checker.jar")
}

configurations.all {
    resolutionStrategy.eachDependency { DependencyResolveDetails details ->
        if (details.requested.group == 'org.eclipse.platform' &&
            details.requested.name == 'org.eclipse.swt.${osgi.platform}') {
            details.useTarget group: 'org.eclipse.platform', name: 'org.eclipse.swt.gtk.linux.x86_64', version: details.requested.version
        }
    }
}

